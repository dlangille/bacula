######################################################################
#
#  !!!!!!! IMPORTANT !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#
#  Edit only tray-monitor.pro.mingw32.in  -- tray-monitor.pro.mingw32 is built by the ./configure program
#
#  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#    
# CONFIG options for Windows are pulled from win32/qmake.conf
#
# Copyright (C) 2000-2020 Kern Sibbald
# License: BSD 2-Clause; see file LICENSE-FOSS
#

CONFIG += qt cross-win32
#CONFIG += qt debug 

QT += widgets core gui

cross-win32 {
  LIBS       +=  ../../win32/lib/obj32/ini.o -mwindows -L../../win32/release32 -lbacula -lwinpthread
  INCLUDEPATH += ../../win32/compat
}
!cross-win32 {
  LIBS        += -L../../lib -lbaccfg -lbac -L../../findlib -lbacfind @OPENSSL_LIBS@
}


bins.path = /$(DESTDIR)@sbindir@
bins.files = bacula-tray-monitor
confs.path = /$(DESTDIR)@sysconfdir@
confs.commands = ./install_conf_file

TEMPLATE     = app
TARGET       = bacula-tray-monitor
QMAKE_EXTRA_TARGETS += depend
DEPENDPATH  += .
INCLUDEPATH += ../.. . 
LIBTOOL_LINK = @QMAKE_LIBTOOL@ --silent --tag=CXX --mode=link
LIBTOOL_INSTALL = @QMAKE_LIBTOOL@ --silent --mode=install
QMAKE_LINK   = $${LIBTOOL_LINK} $(CXX)
QMAKE_INSTALL_PROGRAM = $${LIBTOOL_INSTALL} install -m @SBINPERM@ -p
QMAKE_CLEAN += .libs/* bacula-tray-monitor release/bacula-tray-monitor
QMAKE_CXXFLAGS += -DTRAY_MONITOR
QMAKE_CFLAGS += -DTRAY_MONITOR


RESOURCES               = ../main.qrc
MOC_DIR                 = moc32
OBJECTS_DIR             = obj32
UI_DIR                  = ui32
QMAKE_CC                = $(CXX)
QMAKE_CXX               = i686-w64-mingw32-g++
QMAKE_INCDIR            = $(DEPKGS)/depkgs-mingw32/include/ ../win32/compat $(DEPKGS)/depkgs-mingw32/include/QtGui $(DEPKGS)/depkgs-mingw32/include/QtCore $(DEPKGS)/depkgs-mingw32/include/QtWidgets ui32/
QMAKE_INCDIR_QT         = $(DEPKGS)/depkgs-mingw32/include/qt
QMAKE_LIBDIR_QT         = $(DEPKGS)/depkgs-mingw32/lib/qt
QMAKE_LINK              = i686-w64-mingw32-g++
QMAKE_LFLAGS            = -mthreads -Wl,-enable-stdcall-fixup -Wl,-enable-auto-import  -m32 -fno-strict-aliasing -Wl,-enable-runtime-pseudo-reloc
QMAKE_LIB               = i686-w64-mingw32-ar -ru
QMAKE_RC                = i686-w64-mingw32-windres

# Main directory
HEADERS += tray-monitor.h tray_conf.h tray-ui.h fdstatus.h task.h ../util/fmtwidgetitem.h dirstatus.h conf.h sdstatus.h runjob.h status.h restorewizard.h filesmodel.h clientselectwizardpage.h jobselectwizardpage.h fileselectwizardpage.h restoreoptionswizardpage.h pluginwizardpage.h
SOURCES +=  tray-monitor.cpp tray_conf.cpp fdstatus.cpp task.cpp authenticate.cpp ../util/fmtwidgetitem.cpp dirstatus.cpp sdstatus.cpp conf.cpp runjob.cpp status.cpp restorewizard.cpp clientselectwizardpage.cpp jobselectwizardpage.cpp fileselectwizardpage.cpp restoreoptionswizardpage.cpp pluginwizardpage.cpp
FORMS += fd-monitor.ui dir-monitor.ui sd-monitor.ui main-conf.ui res-conf.ui run.ui restorewizard.ui clientselectwizardpage.ui jobselectwizardpage.ui fileselectwizardpage.ui restoreoptionswizardpage.ui pluginwizardpage.ui

# CDP Client
CDP_DIR = ../../tools/cdp-client
HEADERS += $$CDP_DIR/backupservice.h $$CDP_DIR/folderwatcher.h desktop-gui/cdp-main-ui.h $$CDP_DIR/cdp.h
SOURCES += $$CDP_DIR/backupservice.cpp $$CDP_DIR/folderwatcher.cpp
INCLUDEPATH += $$CDP_DIR

# Journal
JOURNAL_DIR = ../../plugins/fd
HEADERS += $$JOURNAL_DIR/journal.h $$JOURNAL_DIR/file-record.h $$JOURNAL_DIR/folder-record.h $$JOURNAL_DIR/settings-record.h
SOURCES += $$JOURNAL_DIR/journal.c
INCLUDEPATH += $$JOURNAL_DIR

TRANSLATIONS += ts/tm_fr.ts ts/tm_de.ts ts/tm_ja.ts
